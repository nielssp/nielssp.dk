export CONFIG = {
  time_zone: "Europe/Copenhagen",
  title: "nielssp.dk",
  description: "The blog of a 28 year old software developer",
  copyright: "Niels Sonnich Poulsen",
}

export ROOT_PATH = '/'
export ROOT_URL = 'http://nielssp.dk'

import('modules/highlightjs.plet')
export highlightjs
export import_highlightjs

export IMAGE_PRESERVE_LOSSLESS = false

OUTPUT_OBSERVERS | push(path => do
  if path | lower | ends_with('html')
    exec('htmlmin', path, path)
#  else if path | lower | ends_with('png')
#    exec('pngcrush', path, "{path}.optimized")
#    exec('mv', "{path}.optimized", path)
  end if
end do)

exec('mkdir -p', "{DIST_ROOT}/css")
exec('sassc --style compressed', "{SRC_ROOT}/scss/main.scss", "{DIST_ROOT}/css/main.css")
exec('mkdir -p', "{DIST_ROOT}/js")
exec("slimit {"{SRC_ROOT}/js/gallery.js" | shell_escape} > {"{DIST_ROOT}/js/gallery.js" | shell_escape}")

add_static('favicon.ico')
add_static('robots.txt')

add_page('404.html', 'templates/404.plet.html')

pages = list_content('content/pages', {suffix: '.md'})
for page in pages
  if page.name == 'index'
    page.link = "{page.relative_path}/index.html"
  else
    page.link = "{page.relative_path}/{page.name}/index.html"
  end if
  add_page(page.link, 'templates/page.plet.html', {
    page: page
  })
  add_reverse(page.path, page.link)
end for

published_posts = list_content('content/published', {suffix: '.md'}) | filter(p => p.published?) | sort_by_desc(.published)

tag_groups = {}

for post in published_posts
  for tag in post.tags or []
    if not tag_groups[tag]
      tag_groups[tag] = []
    end if
    tag_groups[tag] | push(post)
  end for
  post.year = post.published | date('%Y')
  post.month = post.published | date('%m')
  post.link = "{post.year}/{post.month}/{post.name}/index.html"
  add_page(post.link, 'templates/post.plet.html', {
    post: post
  })
  add_reverse(post.path, post.link)
end for

add_page('posts.rss', 'templates/posts.plet.rss', {posts: published_posts})

add_page('archive/index.html', 'templates/archive.plet.html', {posts: published_posts})
add_page('tags/index.html', 'templates/tags.plet.html', {posts: published_posts})

for group in published_posts | group_by(.year)
  year = group[0].year
  add_page("{year}/index.html", 'templates/archive-year.plet.html', {
    posts: group,
    year: year,
  })
  for subgroup in group | group_by(.month)
    month = subgroup[0].month
    add_page("{year}/{month}/index.html", 'templates/archive-month.plet.html', {
      posts: subgroup,
      year: year,
      month: month,
    })
  end for
end for

add_page('index.html', 'templates/post-list.plet.html', {
  posts: published_posts,
})

for tag: posts in tag_groups
  add_page("tags/{tag}/index.html", 'templates/archive-tag.plet.html', {
    posts: posts,
    tag: tag,
  })
end for
