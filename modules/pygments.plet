import('html')

export pygmentize = node => do
  if type(node) != 'object'
    return node
  end if
  if node.tag? == symbol('code') and node.attributes?.class?
    language = node.attributes.class | lower
    if language | starts_with('lang-')
      language = language | drop(5)
    else if language | starts_with('language-')
      language = language | drop(9)
    end if
    if language and language != 'nohighlight'
      node.attributes[symbol('data-lang')] = language
      code = text_content(node)
      lexer_arg = "-l {shell_escape(language)}"
      highlighted = "echo {shell_escape(code)} | pygmentize {lexer_arg} -f html -O nowrap=True" | exec
      if highlighted
        node.children = [highlighted | parse_html]
      end if
    end if
  else
    for child in node.children? or []
      pygmentize(child)
    end for
  end if
  return node
end do
