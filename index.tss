export CONFIG = {
  time_zone: "Europe/Copenhagen",
  title: "nielssp.dk",
  description: "The blog of a 27 year old software developer",
  copyright: "Niels Sonnich Poulsen",
}

export ROOT_PATH = '/'
export ROOT_URL = 'http://nielssp.dk'

add_static('favicon.ico')
add_static('robots.txt')

add_page('404.html', 'templates/404.tss.html')

pages = list_content('content/pages', {suffix: '.md'})
for page in pages
  if page.name == 'index'
    page.link = "{page.relative_path}/index.html"
  else
    page.link = "{page.relative_path}/{page.name}/index.html"
  end if
  add_reverse(page.path, page.link)
end for

for page in pages
  add_page(page.link, 'templates/page.tss.html', {
    page: page
  })
end for

published_posts = list_content('content/published', {suffix: '.md'}) | filter(p => p.published?) | sort_by_desc(.published)

tag_groups = {}

for post in published_posts
  for tag in post.tags or []
    if not tag_groups[tag]
      tag_groups[tag] = []
    end if
    tag_groups[tag] | push(post)
  end for
  post.year = post.published | date('%Y')
  post.month = post.published | date('%m')
  post.link = "{post.year}/{post.month}/{post.name}/index.html"
  add_reverse(post.path, post.link)
end for

for post in published_posts
  add_page(post.link, 'templates/post.tss.html', {
    post: post
  })
end for

add_page('posts.rss', 'templates/posts.tss.rss', {posts: published_posts})

add_page('archive/index.html', 'templates/archive.tss.html', {posts: published_posts})

for group in published_posts | group_by(.year)
  year = group[0].year
  group | paginate(5, "{year}%page%/index.html", 'templates/archive-year.tss.html', {
    year: year
  })
  for subgroup in group | group_by(.month)
    month = subgroup[0].month
    subgroup | paginate(5, "{year}/{month}%page%/index.html", 'templates/archive-month.tss.html', {
      year: year,
      month: month
    })
  end for
end for

published_posts | paginate(5, '%page%/index.html', 'templates/post-list.tss.html')

for tag: posts in tag_groups
  posts | sort_by(.published) | paginate(5, "tags/{tag}%page%/index.html", 'templates/archive-tag.tss.html', {
    tag: tag
  })
end for
